buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'com.cinnober.gradle', name: 'semver-git', version: '2.2.0'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.cinnober.gradle.semver-git'

group = "com.namehillsoftware"
archivesBaseName = "lazy-j"
version = project.version

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

signing {
    required { !version.endsWith("SNAPSHOT") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

if (!hasProperty("sonatypeUsername")) {
    sonatypeUsername = ""
}

if (!hasProperty("sonatypePassword")) {
    sonatypePassword = ""
}

uploadArchives(dependsOn: ('signing')) {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
//                name 'lazy-j'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Embarassingly Simple Lazy Instantiation Library'
                url 'http://www.namehillsoftware.com/'

                scm {
                    connection 'https://github.com/danrien/lazy-j.git'
                    developerConnection 'https://github.com/danrien/lazy-j.git'
                    url 'https://github.com/danrien/lazy-j'
                }

                licenses {
                    license {
//                        name 'GNU Lesser General Public License v3.0'
                        url 'https://www.gnu.org/copyleft/lesser.html'
                    }
                }

                developers {
                    developer {
                        id 'david'
//                        name 'David Vedvick'
                        email 'dvedvick@gmail.com'
                    }
                }
            }
        }
    }
}

uploadArchives.setEnabled(!version.endsWith("SNAPSHOT"))