apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.namehillsoftware"
archivesBaseName = "lazy-j"
version = "1.0.1"

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

if (sonatypeUsername != null && sonatypePassword != null) {
    signing {
        sign configurations.archives
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (sonatypeUsername != null && sonatypePassword != null) {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }

            pom.project {
//                name 'lazy-j'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Embarassingly Simple Lazy Instantiation Library'
                url 'http://www.namehillsoftware.com/'

                scm {
                    connection 'https://github.com/danrien/lazy-j.git'
                    developerConnection 'https://github.com/danrien/lazy-j.git'
                    url 'https://github.com/danrien/lazy-j'
                }

                licenses {
                    license {
//                        name 'GNU Lesser General Public License v3.0'
                        url 'https://www.gnu.org/copyleft/lesser.html'
                    }
                }

                developers {
                    developer {
                        id 'david'
//                        name 'David Vedvick'
                        email 'dvedvick@gmail.com'
                    }
                }
            }
        }
    }
}